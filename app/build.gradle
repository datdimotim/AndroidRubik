plugins {
    id "com.gorylenko.gradle-git-properties" version "2.3.1"
}

apply plugin: 'com.android.application'

task generateSomeAppResources {
    ext.outputDir = new File(projectDir, "build/generated/res/raw/")
    doFirst {
        mkdir outputDir
        new File(outputDir, "git.properties").bytes = new File(projectDir, "build/resources/main/git.properties").bytes
    }
}

generateSomeAppResources.dependsOn('generateGitProperties')

gradle.projectsEvaluated {
    preBuild.dependsOn('generateSomeAppResources')
}



android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.dimotim.kubsolver"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        buildConfigField "String", "gitHash", "\""+ /*buildSing+*/ "\""

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "resourcePackageName": "com.dimotim.kubsolver"
                ]
            }
        }
    }

    sourceSets {
        main {
            res.srcDirs += [
                    'build/generated/res',
            ]
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFile 'proguard-rules.txt'
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = applicationName + /*"-" + buildSing + */ ".apk"
                }
            }
        }

        debug {
            minifyEnabled true
            proguardFile 'proguard-rules.txt'
            applicationIdSuffix ".debug"
        }

    }
}


repositories {
    maven {
        url "https://s3.amazonaws.com/repo.commonsware.com"
    }
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.github.datdimotim:KubSolver:3.0.8'
    implementation 'com.commonsware.cwac:layouts:0.4.3'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    implementation "androidx.work:work-runtime:2.6.0"

    implementation('com.journeyapps:zxing-android-embedded:4.1.0') { transitive = false }
    implementation 'com.google.zxing:core:3.3.0'

    implementation 'com.google.code.gson:gson:2.8.6'

    annotationProcessor "org.androidannotations:androidannotations:4.8.0"
    implementation "org.androidannotations:androidannotations-api:4.8.0"

    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
}
